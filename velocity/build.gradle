plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

group = 'it.pintux'
version = '1.7.4'

repositories {
    mavenCentral()
    maven { url 'https://nexus.velocitypowered.com/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = "https://repo.opencollab.dev/main/" }
}

dependencies {
    implementation project(':common')
    compileOnly 'com.velocitypowered:velocity-api:3.2.0-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-api:3.2.0-SNAPSHOT'
    implementation 'org.spongepowered:configurate-yaml:4.1.2'
    compileOnly 'org.geysermc.floodgate:api:2.2.3-SNAPSHOT'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
            srcDirs += project(':common').sourceSets.main.resources.srcDirs
        }
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'

    from(project(':common').sourceSets.main.resources) {
        exclude 'velocity-plugin.json'
    }

    filesMatching('velocity-plugin.json') {
        expand props
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

shadowJar {
    minimize()
    archiveClassifier.set('')
    archiveBaseName.set('BedrockGUI-Velocity')
    //destinationDirectory.set(file("${System.getProperty('user.home')}/Desktop"))
}